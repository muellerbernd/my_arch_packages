# Maintainer: Nxxx <nx dot tardis at gmail dot com>

pkgname=ignition-transport-8
pkgver=8.4.0
pkgrel=2
pkgdesc="The transport library combines ZeroMQ with Protobufs to create a fast and efficient message passing system. Asynchronous message publication and subscription is provided along with service calls and discovery."
arch=('i686' 'x86_64')
url="https://gazebosim.org/libs/transport"
license=('Apache')
groups=('development')
depends=('protobuf' 'protobuf-c' 'zeromq' 'ignition-msgs=5' 'ignition-tools=1'
         'libutil-linux')
makedepends=('ignition-cmake=2' 'util-linux')
provides=('ignition-transport=8')
optdepends=()
conflicts=()
source=("https://github.com/gazebosim/gz-transport/archive/ignition-transport8_${pkgver}.tar.gz")
sha256sums=('65472a874bbb0f6bb686b3c0cce4146fb650e750f8f795a159523ecbe731c1c7')

_dir="gz-transport-ignition-transport8_${pkgver}"

build() {
  cd "${srcdir}/${_dir}"
  mkdir -p build && cd build

  cmake .. -DCMAKE_BUILD_TYPE="Release" \
           -DCMAKE_INSTALL_PREFIX="/usr" \
           -DCMAKE_INSTALL_LIBDIR="lib" \
           -DBUILD_TESTING=Off
  make
}

package() {
  cd "${srcdir}/${_dir}/build"
  make DESTDIR="${pkgdir}/" install
}
