# Maintainer: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Tim Rakowski <tim.rakowski@gmail.com>

pkgname=ignition-cmake-2
pkgname_=ignition-cmake
pkgver=2.17.0
pkgrel=1
pkgdesc="Provides modules that are used to find dependencies of ignition projects and generate cmake targets for consumers of ignition projects to link against."
arch=('any')
url="https://gazebosim.org/libs/cmake"
license=('Apache')
depends=('cmake' 'pkg-config' 'ruby-ronn' 'doxygen' 'cppzmq')
provides=('ignition-cmake=2')
optdepends=()
conflicts=()
source=("https://github.com/gazebosim/gz-cmake/archive/${pkgname_}2_${pkgver}.tar.gz")
sha256sums=('f7953691bbeb09f1e86db89417cf15f34e7e2c025f76925297cf82e1fd17e1cf')

_dir="gz-cmake-${pkgname_}2_${pkgver}"

build() {
  cd "$srcdir/$_dir"

  mkdir -p build
  cd build

  cmake .. -Wno-dev \
           -DCMAKE_BUILD_TYPE="Release" \
           -DCMAKE_INSTALL_PREFIX="/usr" \
           -DCMAKE_INSTALL_LIBDIR="lib" \
           -DBUILD_TESTING=OFF

  make
}

package() {
  cd "$srcdir/$_dir/build"
  make DESTDIR="$pkgdir/" install
}
